buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://maven.minecraftforge.net/' }
        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
        mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
		classpath 'com.integral.grimoire:GrimoireShenanigans:1.2.5' // Has to be this or lower
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
    }
}

project.evaluationDependsOn(":Grimoire-shared")
apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply from: '../artifacts.gradle'
apply from: '../publish.gradle'
apply plugin: 'grimoire-shenanigans'
apply plugin: 'com.matthewprenger.cursegradle'
apply from: '../curseforge.gradle'

archivesBaseName = project.name

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
}

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'
	
	annotationProcessor("org.spongepowered:mixin:0.8.3-SNAPSHOT:processor")
    implementation("org.spongepowered:mixin:0.8.3-SNAPSHOT")
	implementation('org.jetbrains:annotations:20.1.0')
	implementation(project(':Grimoire-shared'))
    
    implementation name: "EventHelper-1.12.2-2.8.1-dummydev"
}

mixin {
    disableAnnotationProcessorCheck()	
	add sourceSets.main, mixinRefmapName
}

tasks.withType(Jar) {
	if (name.equals('jar') || name.equals('devJar')) {
		manifest {
			attributes([
                'FMLCorePlugin': "io.github.crucible.grimoire.mc1_12_2.GrimoireCoremod"
			])
		}
	}
}

jar.finalizedBy('reobfJar')